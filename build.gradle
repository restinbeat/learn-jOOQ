buildscript {
	ext {
		jooqVersion = '3.19.5'
	}
}

plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.0'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'nu.studer.jooq' version '9.0' // gradle plugin
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	// jOOQ 의존성 수동설정
	implementation ('org.springframework.boot:spring-boot-starter-jooq') {
		exclude group: 'org.jooq:jooq'
	}
	implementation "org.jooq:jooq:${jooqVersion}"

	// jOOQ code  generator
	jooqGenerator 'com.mysql:mysql-connector-j'
	jooqGenerator "org.jooq:jooq:${jooqVersion}"
	jooqGenerator "org.jooq:jooq-meta:${jooqVersion}"

	runtimeOnly 'com.mysql:mysql-connector-j'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}


String dbUser   = System.properties["db-user"]   ?: "root"
String dbPasswd = System.properties["db-passwd"] ?: "passwd"

jooq {
	version = "${jooqVersion}"
	configurations {
		sakilaDB {
			generationTool {

				// DB 접속정보
				jdbc {
					driver = 'com.mysql.cj.jdbc.Driver'
					url = 'jdbc:mysql://localhost:3306'
					user = "${dbUser}"
					password = "${dbPasswd}"
				}

				generator {
					name = 'org.jooq.codegen.DefaultGenerator'

					database {
						name = 'org.jooq.meta.mysql.MySQLDatabase'

						schemata {
							schema {
								inputSchema = 'sakila'
							}
						}
					}

					// DSL generate strategy
					/**
					 * https://www.jooq.org/doc/3.19/manual/code-generation/codegen-advanced/ 참고
					 * option			default		description
					 * fluentSetter 	false		pojo나 record 생성시 return 값이 void가 대신 객체 자체를 리턴
					 * javaTimeTypes	false 		Date, Time, Timestamp 대신 -> LocalDate, LocalTime, LocalDateTIme 리텀
					 * unsignedTypes	true		jOOQ에서 제공하는 unsigned type 사용 여부 (미사용 추천, database)
					 * forcedTypes		-			특정 데이터 타입을 DSL에선 강제로 다른타입으로 지정 (eg. INET, JSON -> varchar)
					 * daos				false		DSL 생성시 일부 편의 기능이 완성되어있는 Dao 자동생성
					 * pojos			false		DB 테이블에 대응되는 pojo 자동생성
					 * record			false		DB 테이블에 대응되는 ActiveRecord 자동생성
					 */
					generate {
						daos = true
						records = true
//						immutablePojos = true
						fluentSetters = true
						javaTimeTypes = true
						deprecated = false
					}

					target {
						directory = 'src/generated'
					}
				}
			}
		}
	}
}

sourceSets {
	main {
		java {
			srcDirs = ["src/main/java", "src/generated"]
		}
	}
}